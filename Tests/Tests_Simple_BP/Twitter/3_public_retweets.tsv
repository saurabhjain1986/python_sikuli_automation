*** Settings ***
Library	twitter_api.TwitterApi	WITH NAME	Twitter
Library	wde.WDE	WITH NAME	WDE
Library	DateTime
Library	OperatingSystem
Suite Setup	Start work
Suite Teardown	Run	kill.bat
Variables	twitter_config.py

*** Variables ***
${brand_mention}	${tw_agent_screen_name}
${customer_mention}	${tw_screen_name}

*** Test Cases ***
Customer make new tweet with only text and agent retweet
	[Tags]	SMD-3918
	[Documentation]	TW: Public Tweet: Customer send new tweet - Agent Retweet with only text
	Test Customer make new tweet agent retweet	with only text

Customer make new tweet with image and agent retweet
	[Tags]	SMD-3919
	[Documentation]	TW: Public Tweet: Customer send new tweet - Agent Retweet with image
	Test Customer make new tweet agent retweet	with image

Customer make new tweet with link and agent retweet
	[Tags]	SMD-3920
	[Documentation]	TW: Public Tweet: Customer send new tweet - Agent Retweet with link
	Test Customer make new tweet agent retweet	with link : https://www.google.com

Customer make new tweet with emojis and agent retweet
	[Tags]	SMD-3921
	[Documentation]	TW: Public Tweet: Customer send new tweet - Agent Retweet with emojis
	Test Customer make new tweet agent retweet	with emojis :-) :-D :-(

*** Keywords ***
Return Timestamp
	${ts} =	Get Current Date	result_format=%Y%m%d%H%M%S
	[Return]	${ts}

Start work
	Run Keywords	WDE.Start App	AND	WDE.Do Login	AND	WDE.Set Agent Status Ready	AND	Twitter.Set Twitter Accounts
	WDE.Accept Pending Interactions
	${global_id} =	Run Keyword	Return Timestamp
	Set Suite Variable	${global_id}

Test Customer make new tweet agent retweet
	[Arguments]	${with_option}
	Run Keyword If	'${with_option}' == 'with image'	Customer send new tweet with image	Twitter- Good work, OFA ${global_id} ${with_option}	test.png
	Run Keyword If	'${with_option}' != 'with image'	Customer send new tweet	Twitter- Good work, OFA ${global_id} ${with_option}
	Agent receive and retweet	${with_option}

Customer send new tweet
	[Arguments]	${tweet_text}
	Twitter.Customer Post Tweet	@${brand_mention} ${tweet_text}

Customer send new tweet with image
	[Arguments]	${tweet_text}	${image_name}
	Twitter.Customer Post Tweet With Image	@${brand_mention} ${tweet_text}	${image_name}

Agent receive and retweet
	[Arguments]	${with_option}
	WDE.Accept Incoming Interaction
	WDE.Verify Twitter Interaction Text
	WDE.Agent Click Retweet With Comments
	Run Keyword If	'${with_option}' == 'with image'	WDE.Attach Image	test.png
	WDE.Send Post
	WDE.Mark Interaction Done
	Twitter.Verify Last Status Agent	RT @${customer_mention}: @${brand_mention} Twitter- Good work, OFA ${global_id}